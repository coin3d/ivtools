# *******************************************************************
# ** Misc init/setup stuff.
# *******************************************************************

#  This can be any file in the package.
AC_INIT(ivcat/ivcat.cpp)

#  Locate C++ compiler and set C++ as the default language to use
#  in tests. The configure script will automatically terminate if
#  it doesn't find a C++ compiler.
AC_PROG_CXX
AC_LANG_CPLUSPLUS

AM_INIT_AUTOMAKE(IvTools, 0.9)

#  Files to cram results from the configure tests into.
AM_CONFIG_HEADER(config.h)

#  Turn off default maintainer make-rules -- use ./bootstrap instead.
AM_MAINTAINER_MODE


# *******************************************************************
# ** Check if getopt() is available.
# *******************************************************************

SIM_AC_CHECK_HEADERS(unistd.h) # for getopt()

AC_CACHE_CHECK([whether getopt() is available],
  sim_cv_func_getopt_avail,
  [AC_TRY_LINK([
                #if HAVE_UNISTD_H
                #include <unistd.h>
                #endif /* HAVE_UNISTD_H */
               ],
               [(void)getopt(0, 0L, 0L);],
               sim_cv_func_getopt_avail=yes,
               sim_cv_func_getopt_avail=no)])

if test x"$sim_cv_func_getopt_avail" = xyes; then
  AC_DEFINE(HAVE_GETOPT)
fi


# *******************************************************************
# ** Check if isatty() is available.
# *******************************************************************

AC_CACHE_CHECK([whether isatty() is available],
  sim_cv_func_isatty_avail,
  [AC_TRY_LINK([
                #if HAVE_UNISTD_H
                #include <unistd.h>
                #endif /* HAVE_UNISTD_H */
               ],
               [(void)isatty(0);],
               sim_cv_func_isatty_avail=yes,
               sim_cv_func_isatty_avail=no)])

if test x"$sim_cv_func_isatty_avail" = xyes; then
  AC_DEFINE(HAVE_ISATTY)
fi


# *******************************************************************
# ** Try linking against either libInventor or libCoin.
# *******************************************************************

if test x"$with_inventor" = xyes; then
  # If we want to compile and link against Inventor, we
  # need to check for a shitload of libraries.

  SIM_AC_CHECK_MATHLIB
  SIM_AC_CHECK_DL
  
  SIM_AC_CHECK_OPENGL( , AC_MSG_ERROR(needs an OpenGL-compatible development system installation))
  SIM_AC_CHECK_GLU( , AC_MSG_ERROR(needs the OpenGL utility library))
  SIM_CHECK_INVENTOR( , AC_MSG_ERROR(couldn't compile and link against Open Inventor))
  
else

  # Compile and link on top of Coin.
  SIM_AC_HAVE_COIN_IFELSE(, AC_MSG_ERROR(Couldn't build against Coin library))
  CPPFLAGS="$CPPFLAGS $sim_ac_coin_cppflags"
  LDFLAGS="$LDFLAGS $sim_ac_coin_ldflags"
  LIBS="$LIBS $sim_ac_coin_libs"
fi

# *******************************************************************
# ** Generate output files.
# *******************************************************************

AC_OUTPUT([
        Makefile
        ivNodeKitStructure/Makefile
        ivcat/Makefile
        ivhierarchy/Makefile
        renderos/Makefile
        ])
